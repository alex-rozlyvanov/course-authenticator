buildscript {
    ext {
        set('springCloudVersion', '2021.0.0')
        set('springBootVersion', '2.6.3')
    }
}

plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'org.springframework.cloud.contract' version '3.0.4'
}

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

configurations {
    integrationTestAnnotationProcessor.extendsFrom annotationProcessor
    integrationTestImplementation.extendsFrom implementation
    integrationTestCompileOnly.extendsFrom compileOnly

    contractTestAnnotationProcessor.extendsFrom annotationProcessor
    contractTestImplementation.extendsFrom implementation
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest

contracts {
    baseClassForTests = 'com.goals.course.authenticator.UserControllerBaseTestClass'
    testMode = 'WEBTESTCLIENT'
}

dependencies {

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework:spring-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'org.flywaydb:flyway-core'
    implementation 'com.netflix.archaius:archaius-aws:0.7.7'

    // JAX-B dependencies for JDK 9+
    implementation "jakarta.xml.bind:jakarta.xml.bind-api:2.3.2"
    implementation "org.glassfish.jaxb:jaxb-runtime:2.3.2"

    // FOR DEBUG ONLY
    implementation "io.projectreactor:reactor-tools:3.4.18"

    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'io.r2dbc:r2dbc-postgresql'
    runtimeOnly 'io.r2dbc:r2dbc-pool'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'io.projectreactor:reactor-test:3.4.18'

// integrationTest =================================================================================
    integrationTestCompileOnly 'org.projectlombok:lombok'
    integrationTestAnnotationProcessor 'org.projectlombok:lombok'
    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    integrationTestImplementation 'org.postgresql:postgresql'
    integrationTestImplementation 'org.testcontainers:testcontainers:1.17.2'
    integrationTestImplementation 'org.testcontainers:postgresql:1.17.2'
    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-webflux'

    integrationTestRuntimeOnly 'org.postgresql:postgresql'
    integrationTestRuntimeOnly 'io.r2dbc:r2dbc-postgresql'
    integrationTestRuntimeOnly 'io.r2dbc:r2dbc-pool'

// contractTest =================================================================================
    contractTestImplementation 'org.testcontainers:testcontainers:1.17.2'
    contractTestImplementation 'org.testcontainers:postgresql:1.17.2'
    contractTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    contractTestImplementation 'org.springframework.security:spring-security-test'
    contractTestImplementation 'org.springframework.boot:spring-boot-starter-webflux'
    contractTestImplementation 'io.rest-assured:spring-web-test-client:5.1.1'
    contractTestImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'

}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

jar {
    archiveFileName='default-app.jar'
}

bootJar {
    archiveFileName='boot-app.jar'
}

verifierStubsJar {
    archiveFileName="app-${rootProject.name}-contract-stubs.jar"
}
